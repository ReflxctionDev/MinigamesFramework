import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    repositories {
        maven { url 'https://plugins.gradle.org/m2' }
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

apply plugin: 'java'
apply plugin: 'com.github.johnrengelman.shadow'

def projectName = "MinigamesFramework"
def projectVersion = "1.0-SNAPSHOT"

subprojects {

    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'com.github.johnrengelman.shadow'

    group = 'io.github.superkoth'
    version = projectVersion

    sourceCompatibility = targetCompatibility = 1.8

    each { subproject -> evaluationDependsOn(subproject.path) }

    ext {
        moduleVersion = projectVersion
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
        maven { url 'https://hub.spigotmc.org/nexus/content/groups/public/' }
        maven { url "https://repo.dmulloy2.net/nexus/repository/public/" }
        maven { url 'https://jitpack.io/' }
    }

    dependencies {
        compileOnly 'me.clip:placeholderapi:2.10.9'
        compileOnly 'com.github.ReflxctionDev:PluginLib:1.2'
        compileOnly group: 'com.github.cryptomorin', name: 'XSeries', version: '7.6.1'
        compileOnly group: 'com.google.code.gson', name: 'gson', version: '2.8.6'
        compileOnly group: 'org.jetbrains', name: 'annotations', version: '16.0.1'
        compileOnly 'org.projectlombok:lombok:1.18.16'
        annotationProcessor 'org.projectlombok:lombok:1.18.16'
    }

    jar {
        from '../LICENSE'
    }

}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        filter ReplaceTokens, tokens: [version: version]
    }
}

task assembleJar(type: Jar, dependsOn: subprojects.tasks['classes']) {
    archiveBaseName.set(projectName + "-" + projectVersion)
    for (subproject in subprojects) {
        if (subproject.name == "registry-processor") continue
        from subproject.configurations.archives.allArtifacts.files.collect {
            it = new File(it.parentFile, it.name.replace(".jar", "-all.jar"))
            zipTree(it) // include all sub-projects in the final JAR
        }
    }
}

tasks.assembleJar.dependsOn tasks.shadowJar

artifacts {
    archives(assembleJar)
}

build.dependsOn shadowJar